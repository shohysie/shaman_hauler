BACKUP ~shaman_hauler/backup~
AUTHOR ~shohy@126.com~

VERSION ~v0.2~
AUTO_EVAL_STRINGS

README ~%MOD_FOLDER%/README.%LANGUAGE%.txt~
AUTO_TRA ~%MOD_FOLDER%/tra/%s~

LANGUAGE  ~American English~	~english~ 		~shaman_hauler/tra/english/game.tra~ ~shaman_hauler/tra/english/setup.tra~
LANGUAGE  ~Simplified Chinese~	~schinese~  ~shaman_hauler/tra/schinese/game.tra~ ~shaman_hauler/tra/schinese/setup.tra~

BEGIN @1
REQUIRE_PREDICATE GAME_IS ~bgee bg2ee eet~ OR FILE_EXISTS_IN_GAME BD1000.are @20	//版本不对

//SHAM#HL0.bam	空bam
//SHAM#HLA.bam	萨满舞蹈图标
//SHAM#HLB.bam	萨满舞蹈图标改
//SHAM#HLC.bam	搬运图标
//SHAM#HLD.bam	搬运图标改
COPY ~shaman_hauler/bam~ ~override~

//SHAM#HLA/B.cre	鸟，承载脚本
COPY ~shaman_hauler/cre~ ~override~

//SHAM#HL1.spl	萨满新增技能，给其他人加上技能SHAM#HL3
COPY ~shaman_hauler/spl/SHAM#HL1.spl~ ~override~
SAY NAME1 @2
SAY UNIDENTIFIED_DESC @3

//SHAM#HL2.spl	萨满新增高阶技能，给其他人加上技能SHAM#HL4
COPY ~shaman_hauler/spl/SHAM#HL2.spl~ ~override~
SAY NAME1 @4
SAY UNIDENTIFIED_DESC @5

//SHAM#HL3.spl	普通搬运，全局变量SHAM#HL1和局部变量SHAM#HLA变1，延迟6秒再SHAM#HL5.spl恢复此技能
COPY ~shaman_hauler/spl/SHAM#HL3.spl~ ~override~
SAY NAME1 @6
SAY UNIDENTIFIED_DESC @7

//SHAM#HL4.spl	传送搬运，延迟1秒传送自身，全局变量SHAM#HL2和局部变量SHAM#HLB变1，调用SHAM#HL6.spl
COPY ~shaman_hauler/spl/SHAM#HL4.spl~ ~override~
SAY NAME1 @8
SAY UNIDENTIFIED_DESC @9

//SHAM#HL5.spl	永久给SHAM#HL3.spl，局部变量SHAM#HLA归零
//SHAM#HL6.spl	延迟6秒调用SHAM#HL9.spl，为了不和SHAM#HL4.spl一起被SHAM#HL7.spl移除
//SHAM#HL7.spl	移除SHAM#HL4.spl打断传送
//SHAM#HL8.spl	局部变量归零
//SHAM#HL9.spl	永久给SHAM#HL4.spl
COPY ~shaman_hauler/spl/noname~ ~override~

//SHAM#HLA/B.baf	鸟的脚本
COMPILE ~shaman_hauler/baf/SHAM#HLA.baf~
COMPILE ~shaman_hauler/baf/SHAM#HLB.baf~

//全局脚本产生鸟
EXTEND_BOTTOM ~baldur.bcs~ ~shaman_hauler/baf/SHAM#HL.baf~
EXTEND_BOTTOM ~baldur25.bcs~ ~shaman_hauler/baf/SHAM#HL.baf~

ACTION_IF FILE_EXISTS_IN_GAME bdbaldur.bcs THEN BEGIN	
	EXTEND_BOTTOM ~bdbaldur.bcs~ ~shaman_hauler/baf/SHAM#HL.baf~
END

//萨满升级获得技能
APPEND ~CLABSH01.2da~ ~ABILITYS GA_SHAM#HL1 **** **** **** **** **** **** **** **** **** **** **** **** CDREPLACE~
COPY_EXISTING ~CLABSH01.2da~ ~override~
  COUNT_2DA_COLS cols
  FOR (index = 15 ; index < cols ; ++index) BEGIN
    REPLACE_TEXTUALLY ~CDREPLACE~ ~**** CDREPLACE~
  END
  REPLACE_TEXTUALLY ~CDREPLACE~ ~****~
  PRETTY_PRINT_2DA

//萨满高阶技能
COPY_EXISTING ~lush0.2da~ ~override~
  COUNT_2DA_ROWS ~9~ "rows"	//至少要9列。行数读入rows
  SET "patch" = 0
  FOR ( index = 1 ; index < rows ; index = index + 1 ) BEGIN
    READ_2DA_ENTRY "%index%" 1 9 "abil"	//循环读取第二列每个值
    PATCH_IF ("%abil%" STRING_COMPARE_CASE "*" = 0) BEGIN	//如果这一列是空的
      PATCH_IF ("%patch%" = 0) BEGIN
        SET_2DA_ENTRY "%index%" 1 9 ~GA_SHAM#HL2~
        SET_2DA_ENTRY "%index%" 4 9 ~1~
        SET_2DA_ENTRY "%index%" 5 9 ~99~
        SET_2DA_ENTRY "%index%" 6 9 ~1~
        SET "patch" = 1
      END ELSE BEGIN
        SET "index" = "%rows%" // kills loop
      END
    END
  END
BUT_ONLY

